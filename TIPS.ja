■■ browse-url

   以下のように設定しておくと，URI に類似した文字列がある場所で C-x m と
   入力すれば，emacs-w3m で表示されるようになります．また Emacs21 などに
   附属するhtml-mode で C-c C-v とすれば編集の HTML ファイルを emacs-w3m 
   で preview できます．

(setq browse-url-browser-function 'w3m-browse-url)
(autoload 'w3m-browse-url "w3m" "Ask a WWW browser to show a URL." t)
(global-set-key "\C-xm" 'browse-url-at-point)

   もしも，w3m-mode のバッファ内部で C-x m と打鍵した場合には，emacs-w3m 
   を利用せずに，直接，外部ブラウザを呼び出したい場合には，以下のように
   設定してください．

(defadvice browse-url-at-point
  (around change-browse-url-browser-function activate compile)
  (let ((browse-url-browser-function
         (if (eq major-mode 'w3m-mode)
             'browse-url-netscape
           'w3m-browse-url)))
    ad-do-it))



■■ dired

   以下のように設定しておくと，dired-mode のバッファでファイルを選択して
   いる状態で C-x m と入力すれば，該当ファイルが emacs-w3m で表示される
   ようになります．

(add-hook 'dired-mode-hook
          (lambda ()
            (define-key dired-mode-map "\C-xm" 'dired-w3m-find-file)))
(defun dired-w3m-find-file ()
  (interactive)
  (require 'w3m)
  (let ((file (dired-get-filename)))
    (if (y-or-n-p (format "Open 'w3m' %s " (file-name-nondirectory file)))
        (w3m-find-file file))))



■■ hnf-mode

   以下のように設定しておくと，hnf-mode にて C-c C-b と入力すれば，
   emacs-w3m で最新の日記が表示されるようになります．

(autoload 'w3m-browse-url "w3m" nil t)
(defun w3m-hnf-browse-url-w3m (url &optional new-window)
  (interactive (browse-url-interactive-arg "URL: "))
  (save-selected-window
    (pop-to-buffer (get-buffer-create "*w3m*"))
    (w3m-browse-url url new-window)))
(setq hnf-browse-url-browser-function (function w3m-hnf-browse-url-w3m))



■■ Gnus

   Oort Gnus や Gnus v5.8.8, v5.9 でも，最新の T-gnus 6.15 に含まれてい
   る nnshimbun.el を lisp/ ディレクトリにコピーしてから make install を
   行なうと，nnshimbun で記事を読むことができるようになります．加えて，
   text/html のコンテンツの表示に Emacs-W3 ではなくて emacs-w3m を使うに
   は，以下の設定を .gnus ファイルに追加して下さい．

;; From: greg@visiontech-dml.com
;; Newsgroups: gnu.emacs.help
;; Subject: Re: w3m-mode and images
;; Date: 19 Jul 2001 10:59:19 +0300
;; Message-ID: <2fasnftcpt4.fsf@broadcom.com>

(defvar gnus-w3m-minor-mode nil)

(make-variable-buffer-local 'gnus-w3m-minor-mode)
(add-to-list 'minor-mode-alist '(gnus-w3m-minor-mode " w3m"))
(add-to-list 'minor-mode-map-alist (cons 'gnus-w3m-minor-mode w3m-mode-map))

(defadvice mm-inline-text (around use-w3m-instead (handle) activate)
  (let ((type (mm-handle-media-subtype handle)))
    (if (not (equal type "html"))
	ad-do-it
      (let ((text (mm-get-part handle))
	    (b (point)))
	(save-excursion
	  (insert text)
	  (save-restriction
	    (narrow-to-region b (point))
	    (goto-char (point-min))
	    (w3m-region (point-min) (point-max))
	    (setq gnus-w3m-minor-mode t))
	  (mm-handle-set-undisplayer
	   handle
	   `(lambda ()
	      (let (buffer-read-only)
		(setq gnus-w3m-minor-mode nil)
		(if (functionp 'remove-specifier)
		    (mapcar (lambda (prop)
			      (remove-specifier
			       (face-property 'default prop)
			       (current-buffer)))
			    '(background background-pixmap foreground)))
		(delete-region ,(point-min-marker)
			       ,(point-max-marker))))))))))

;; Date: Fri, 27 Jul 2001 12:51:12 +0900
;; Message-ID: <yosuwv4v3u8f.fsf@jpl.org>
;; From: Katsumi Yamaoka <yamaoka@jpl.org>
;; To: semi-gnus-ja@meadowy.org
;; Subject: [nnshimbun] toggle inline images

;; 小泉メルマガなどの multipart/related な絵を見る．
;; 表示される場所に難あり．^^;;

(eval-after-load "gnus-art"
  '(or (assoc "multipart/related" gnus-mime-multipart-functions)
       (setq gnus-mime-multipart-functions
	     (cons
	      (cons
	       "multipart/related"
	       (byte-compile
		(lambda (handle)
		  (gnus-mime-display-mixed (cdr handle)))))
	      gnus-mime-multipart-functions))))



■■ yahtml-mode

   以下のように設定しておくと，yahtml-mode で編集中のファイルを preview 
   する時に emacs-w3m を利用することができます．

(autoload 'w3m-goto-url "w3m")
(defadvice yahtml-browse-html
  (around w3m-yahtml-browse-html activate compile)
  (w3m-goto-url (ad-get-arg 0) t))


Local Variables:
mode: indented-text
coding: euc-japan-unix
use-kuten-for-period: nil
use-touten-for-comma: nil
fill-column: 72
End:
